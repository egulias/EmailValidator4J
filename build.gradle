
plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'

version = '1.0.3'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'com.tngtech.java', name: 'junit-dataprovider', version: '1.9.2'
    testCompile "org.mockito:mockito-core:1.+"
}

def siteUrl = 'https://github.com/egulias/EmailValidator4J'
def gitUrl = 'https://github.com/egulias/EmailValidator4J.git'
group = "com.egulias"


String getProperty (String name, String value) {
    project.hasProperty (name)? project.property (name) : value
}

allprojects {
    apply plugin: 'java'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

task createPom {
    doLast {
        pom {
            project {
                groupId 'emailvalidator4j'
                artifactId "emailvalidator4j-${project.version}"
                version project.version

                inceptionYear '2015'
                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo("pom.xml")
    }
}

bintray {
    user = getProperty("bintray.user", "anon")
    key = getProperty("bintray.apikey", "anon")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "EmailValidator4J"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        labels = ['email', 'validator', 'email validation']
        issueTrackerUrl = 'https://github.com/egulias/EmailValidator4J/issues'
        publish = true
        version {
            name = '1.0.3' //Bintray logical version name
            desc = 'Improvements on hyphened domains'
            vcsTag = '1.0.3'
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourceJar
}
